#########################################
# prompt/modules/exec_time
# Author: Rui Pinheiro
#
# Zsh prompt "exec_time" module

function mkprompt_exec_time {
	# Generate prompt message
	function _prompt_exec_time_update {
		local stop=$EPOCHSECONDS
		local start=${_prompt_cmd_timestamp:-$stop}
		integer elapsed=$stop-$start
		if (($elapsed > ${ZSH_PROMPT_MIN_EXEC_TIME:-5})); then
			typeset -g _prompt_exec_time_msg="$( human_readable_seconds $elapsed )"
		else
			typeset -g _prompt_exec_time_msg=""
		fi
		unset elapsed
		unset _prompt_cmd_timestamp # Reset timestamp
	}

	function _prompt_exec_time_preexec {
		typeset -g _prompt_cmd_timestamp=$EPOCHSECONDS
	}

	add-zsh-hook preexec _prompt_exec_time_preexec
	add-zsh-hook precmd _prompt_exec_time_update

	# Add message to the prompt
	mkprompt_add_var "$1" "_prompt_exec_time_msg"
}
