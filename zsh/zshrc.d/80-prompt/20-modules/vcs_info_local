#########################################
# prompt/modules/vcs_info
# Author: Rui Pinheiro
#
# Zsh prompt "vcs_info" module

function mkprompt_vcs_info_local {
	# vcs_info module
	autoload -Uz vcs_info
	zstyle ':vcs_info:*' enable git svn

	zstyle ':vcs_info:*' formats "%b%c%u"
	zstyle ':vcs_info:*' actionformats "%b%c%u (%a:%m)"
	zstyle ':vcs_info:*' unstagedstr "%{$2%}!%{$1%}"
	zstyle ':vcs_info:*' stagedstr "%{$2%}+%{$1%}"
	zstyle ':vcs_info:git:*' patch-format '%7>>%p%<< %n/%a'
	zstyle ':vcs_info:git:*' nopatch-format '%b %n/%a'
	zstyle ':vcs_info:*' check-for-changes true

	# Reload VCS information before every prompt for local filesystems
	function _prompt_vcs_info_local {
		if is_local_fs "$PWD" ; then
			vcs_info
			typeset -g _prompt_vcs_info_local_msg="$vcs_info_msg_0_"
		else
			typeset -g _prompt_vcs_info_local_msg=""
		fi
	}
	add-zsh-hook precmd _prompt_vcs_info_local

	# Add vcs_info information to the prompt
	mkprompt_add_var "$1" "_prompt_vcs_info_local_msg"
}
