########################################
# prompt/init
# Author: Rui Pinheiro
#
# Prompt utilities

autoload -Uz add-zsh-hook

function mkprompt_start {
	# Prompt-related shell options
	setopt promptsubst
	setopt prompt_cr

	# Initialize mkprompt environment
	export MKPROMPT_VAL=""
	export MKPROMPT_NEXT_DELIM=""
	export MKPROMPT_LEFT=1

	# Helper functions
	function mkprompt_add {
		local color="$1"
		local value="$2"
		local delim="$3"

		if [[ ! -z "$value" ]]; then
			if [[ "$#" -lt "3" ]]; then
				delim=" "
			fi

			local add
			if [[ ! -z "$color" ]]; then
				add="%{${color}%}${value}%{${reset_color}%}"
			else
				add="${value}"
			fi

			if is_true "$MKPROMPT_LEFT" ; then
				MKPROMPT_VAL+="${MKPROMPT_NEXT_DELIM}${add}"
			else
				MKPROMPT_VAL="${add}${MKPROMPT_NEXT_DELIM}${MKPROMPT_VAL}"
			fi
			MKPROMPT_NEXT_DELIM="$delim"
		fi
	}

	function mkprompt_left {
		MKPROMPT_LEFT=1
	}
	function mkprompt_right {
		MKPROMPT_LEFT=0
	}

	function mkprompt_text {
		MKPROMPT_VAL+="$@"
	}

	function mkprompt_delim {
		if is_true "$MKPROMPT_NEED_DELIM" ; then
			MKPROMPT_VAL+="$@"
		fi
	}
	function mkprompt_no_delim {
		MKPROMPT_NEED_DELIM=0
	}

	function mkprompt_save {
		local var_name="$1"
		export "$var_name"="$MKPROMPT_VAL"
		MKPROMPT_VAL=""
		MKPROMPT_NEXT_DELIM=""
	}

	function mkprompt_finish {
		unset -m "MKPROMPT_*"
		unfunction -m "mkprompt_*"
	}
}
