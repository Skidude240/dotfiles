########################################
# prompt/init
# Author: Rui Pinheiro
#
# Prompt utilities

autoload -Uz add-zsh-hook

# Prompt-related shell options
setopt promptsubst
setopt prompt_cr

# Initialize mkprompt environment

# Helper functions
function mkprompt_init {
	typeset -g  _mkprompt_default_delim=" "
	typeset -g  _mkprompt_init=1
	typeset -g  _mkprompt_vars=()
	typeset -g  _mkprompt_var
	typeset -g  _mkprompt_arr
	typeset -gA _mkprompt_lefts
	typeset -g  _mkprompt_left
	typeset -gA _mkprompt_next_delim
	typeset -gA _mkprompt_next_delim_chr
	typeset -gA _mkprompt_next_delim_var
}

function mkprompt_start {
	is_false "$_mkprompt_init" && mkprompt_init
	[[ ! -z "$_mkprompt_var" ]] && mkprompt_save

	if (( ! ${_mkprompt_vars[(I)$1]} )); then
		_mkprompt_vars+="$1"
		_mkprompt_arr=()
		_mkprompt_left="${2:-1}"
		_mkprompt_lefts[$1]="$_mkprompt_left"
		_mkprompt_next_delim[$1]=0

		if is_true "$_mkprompt_left" ; then
			_mkprompt_next_delim_chr[$1]=""
			_mkprompt_next_delim_var[$1]=""
		fi
	else
		_mkprompt_arr=("${(P)1}")
		_mkprompt_left="$_mkprompt_lefts[$1]"
	fi
	_mkprompt_var="$1"
}

function mkprompt_apply_delim {
	local new_delim_chr="${1:-$_mkprompt_default_delim}" new_delim_var="$2"

	local next_delim="$_mkprompt_next_delim[$_mkprompt_var]"
	if is_true "$next_delim" ; then
		local delim_var
		local delim_chr

		if is_true "$_mkprompt_left" ; then
			delim_var="$_mkprompt_next_delim_var[$_mkprompt_var]"
			delim_chr="$_mkprompt_next_delim_chr[$_mkprompt_var]"
		else
			delim_var="$new_delim_var"
			delim_chr="$new_delim_chr"
		fi

		if [[ ! -z "$delim_chr" ]]; then
			local delim="$delim_chr"
			[[ ! -z "$delim_var" ]] && delim="\${${delim_var}:+${delim_chr}}"
			_mkprompt_arr+=("$delim")
		fi
	fi

	_mkprompt_next_delim[$_mkprompt_var]=0
	if is_true "$_mkprompt_left" ; then
		_mkprompt_next_delim_chr[$_mkprompt_var]="$new_delim_chr"
		_mkprompt_next_delim_var[$_mkprompt_var]="$new_delim_var"
	fi
}

function mkprompt_add {
	local color="$1" value="$2"
	local new_delim_chr="${3:-$_mkprompt_default_delim}" new_delim_var="$4"

	if [[ ! -z "$value" ]]; then
		local reset=""
		if [[ ! -z "$color" ]]; then
			color="%{$color%}"
			reset="%{$reset_color%}"
		fi
		local add="${color}${value}${reset}"

		mkprompt_apply_delim "$new_delim_chr" "$new_delim_var"
		_mkprompt_arr+=("${add}")

		_mkprompt_next_delim[$_mkprompt_var]=1
	fi
}

function mkprompt_add_var {
	local color="$1" var="$2"

	mkprompt_add "$color" "\${${var}}" "" "${var}"
}

function mkprompt_text {
	mkprompt_add "$1" "${@:2}"
}

function mkprompt_delim {
	_mkprompt_next_delim_chr[$_mkprompt_var]="$@"
}
function mkprompt_no_delim {
	_mkprompt_next_delim[$_mkprompt_var]=0
}

function mkprompt_save {
	local arr
	if is_true "$_mkprompt_left"; then
		arr=(${_mkprompt_arr})
	else
		arr=(${(@Oa)_mkprompt_arr})
	fi
	export "$_mkprompt_var"="${(j::)arr}"
}

function mkprompt_finish {
	mkprompt_save
	#unset -m "_mkprompt_*"
	#unfunction -m "mkprompt_*"
}
