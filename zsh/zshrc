#########################################
# zshrc
# Author: Rui Pinheiro
#
# Zsh configuration file

export USE_ZSH="1"

# Main zsh + dotfiles configuration path
export ZSH_HOME=$(dirname "$(readlink -f "${(%):-%N}")" )
export DOTFILES=$(readlink -f "$ZSH_HOME/..")
hash -d zsh="$ZSH_HOME"
hash -d dotf="$DOTFILES"

# Local zsh configuration
if [[ -d "$HOME/dotfiles_local" ]]; then
	export LOCAL_DOTFILES="$HOME/dotfiles_local"
	hash -d ldotf="$LOCAL_DOTFILES"
fi

# Source zshrc.d files
if [[ -z "$LOCAL_DOTFILES" || ! -d "$LOCAL_DOTFILES/zsh/zshrc.d" ]]; then
    # If no local dotfiles exist, use shell globbing directly
    source "$ZSH_HOME/zshrc.d/"**/*.zsh
else
{
    # Collect files
    local sourcefiles=($ZSH_HOME/zshrc.d/**/*.zsh)
    sourcefiles+=($LOCAL_DOTFILES/zsh/zshrc.d/**/*.zsh)

    # Remove zshrc.d path prefixes
    sourcefiles=(${${sourcefiles##"$ZSH_HOME/zshrc.d/"}#"$LOCAL_DOTFILES/zsh/zshrc.d/"})

    # Sort and remove duplicates
    sourcefiles=(${(@ui)sourcefiles})

    # Source files
    for f in $sourcefiles ; do
        if [[ -f "$ZSH_HOME/zshrc.d/$f" ]]; then
            source "$ZSH_HOME/zshrc.d/$f"
        fi

        if [[ -f "$LOCAL_DOTFILES/zsh/zshrc.d/$f" ]]; then
            source "$LOCAL_DOTFILES/zsh/zshrc.d/$f"
        fi
    done
    unset f
}
fi
